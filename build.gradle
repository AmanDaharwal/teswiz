buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "com.github.vidstige:jadb:$project.jadbVersion"
        classpath fileTree(dir: 'libs', include: ['*.jar'])
    }
}

plugins {
    id "org.sonarqube" version "2.8"
    id "java"
    id "idea"
    id "maven"
    id "groovy"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'com.github.znsio'
version '0.0.3'
project.ext.currentDateTime = new Date().format('MM-dd-HH-mm-ss')
project.ext.log4jProperties = "src/test/resources/log4j2.properties"

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    maven { url "http://repo.bodar.com" }
    maven { url 'https://jitpack.io' }
    maven { url "https://dl.bintray.com/epam/reportportal" }
    maven { url 'https://jitpack.io' }
    jcenter()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

compileJava { options.encoding = "UTF-8" }

dependencies {
    implementation fileTree(dir: "$project.projectDir/libs", include: ['*.jar'])
    implementation files("$buildDir/classes/main")
    implementation files("$buildDir/classes/test")

    implementation "org.codehaus.groovy:groovy-all:+"
    implementation "com.googlecode.json-simple:json-simple:$project.googleCodeJsonSimpleVersion"
    implementation "org.jsoup:jsoup:$project.jsoupVersion"
    implementation "com.mashape.unirest:unirest-java:$project.unirestVersion"
    implementation "org.assertj:assertj-core:$project.assertJVersion"
    implementation "com.github.znsio:AppiumTestDistribution:$project.atdVersion"
    implementation "org.apache.commons:commons-lang3:$project.commonsLang3Version"
    implementation "io.github.bonigarcia:webdrivermanager:$project.webDriverManagerVersion"
    implementation "com.github.vidstige:jadb:$project.jadbVersion"
    implementation 'com.epam.reportportal:agent-java-cucumber6:5.0.1'
    implementation('com.epam.reportportal:agent-java-testng:5.0.7') {
        exclude group: 'com.fasterxml.jackson.core'
    }
    implementation "com.applitools:eyes-connectivity-java3-jersey1x:$project.applitoolsVersion"
    implementation("com.applitools:eyes-selenium-java3:$project.applitoolsVersion") {
        exclude group: 'java-client'
        exclude group: 'selenium-java'
        exclude group: 'eyes-connectivity-java3-jersey2x'
    }
    implementation("com.applitools:eyes-appium-java3:$project.applitoolsVersion") {
        exclude group: 'java-client'
        exclude group: 'selenium-java'
        exclude group: 'eyes-connectivity-java3-jersey2x'
    }
}

clean.doFirst {
    println "********************* Clean.doFirst - start *********************"
    delete "${buildDir}"
    println "Deleted dir - ${buildDir}"
    println "********************* Clean.doFirst - finish *********************"
    println ""
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

shadowJar {
    archiveBaseName.set("$project.name")
    archiveClassifier.set('')
    archiveVersion.set("$version")
}

task fatJar(type: ShadowJar) {
    manifest {
        attributes 'Implementation-Title': 'unified-e2e framework with support for Native App and Browser automation',
                'Implementation-Version': "$project.version"
    }
    from {
        from sourceSets.main.output
        configurations = [project.configurations.cucumberRuntime, project.configurations.compile]
    }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}